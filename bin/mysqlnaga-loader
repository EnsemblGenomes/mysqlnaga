#!/usr/bin/perl

use strict;
use warnings;

our $VERSION = '1.0';

use Carp;
use DBI;
use File::Find;
use File::Spec;
use Getopt::Long;
use Pod::Usage;
use Tie::File;
use Memoize;

memoize('_columns');

my %BINARY_COLUMNS = (
  TINYBLOB => 1,
  BLOB => 1,
  MEDIUMBLOB => 1,
  LONGBLOB => 1,
  BINARY => 1,
  BIT => 1,
  BINARY => 1
);


my $LOG_FILENAME = 'LOADER-LOG.txt';

my %OPTIONS = _parse_options();

my $DB  = $OPTIONS{database};
my $LTB = $OPTIONS{'lines-terminated-by'};
my $FTB = $OPTIONS{'fields-terminated-by'};
my $FEB = $OPTIONS{'fields-optionally-enclosed-by'};
my $VERBOSE = $OPTIONS{verbose};

my $DBH = _open_dbh();
my %DB_TABLES = _db_tables();
my %TABLES;

my ($_record_file) = _find_in_dir(qr/^\${LOG_FILENAME}/xms);
if(! $_record_file) {
	$_record_file = File::Spec->catfile($OPTIONS{directory}, $LOG_FILENAME);
}
tie my @RECORDS, 'Tie::File', $_record_file or confess("Cannot open file $_record_file : $!");
my %PROCESSED_RECORDS = _process_records();

process_db();

untie @RECORDS;

exit 0;

sub process_db {
	verbose("Working on database '${DB}'");
	
	if(scalar(keys %DB_TABLES) == 0) {
		verbose("\tAbout to load schema");
		_squirt_sql();
		%DB_TABLES = _db_tables(); #Have to reload to see what we got from the squirt
	}
	else {
		verbose("\tTables detected in the target schema; not loading the schema file");
	}
	
	%TABLES = _tables();
	
	foreach my $table (sort keys %TABLES) {
		_process_table($table);	
	}
	
	verbose("Finished with database '${DB}'");
	return;
}

sub verbose {
	if($VERBOSE) {
		print STDERR @_, "\n";	
	}
	return;
}

sub _process_table {
	my ($table) = @_;
	
	if($PROCESSED_RECORDS{$table}) {
		verbose("\tNot processing $table as it is already in ${LOG_FILENAME}. If this is not wrong then remove & rerun");
		return;
	}
	
	verbose("\tWorking on table '${table}'");
	
	_nuke($table);
	
	my @files = _find_in_dir(qr/^$table\./xms);
	foreach my $file (@files) {
		verbose("\t\tLoading $file");
		_load($table, $file);
	}
	
	push(@RECORDS, $table);
	
	verbose("\tFinished table");
	return;
}

sub _parse_options {
	my @options = qw(
		directory=s
		database=s 
		fields-escaped-by=s 
		fields-optionally-enclosed-by=s 
		fields-terminated-by=s 
		host=s
		ignore
		lines-terminated-by=s 
		nuke
		password=s
		port=i
		replace
		skip-lines=i
		tables=s
		turn-off-indexes
		user=s
		verbose 
		version 
		help|? 
		man
	);
	
	my $opts = {};
	
	GetOptions($opts, @options) or pod2usage(2);
	pod2usage(1) if $opts->{help};
	pod2usage(-exitstatus => 0, -verbose => 2) if $opts->{man};
	if($opts->{version}) {
		print "mysqlnaga-loader version $VERSION\n";
		exit(0);
	}
	_confirm_options($opts);
	return %{$opts};
}

sub _confirm_options {
	my ($opts) = @_;
	
	if(!$opts->{database}) {
		print STDERR "Need a schema to load\n";
		pod2usage(1);
	}
	
	if($opts->{replace} && $opts->{ignore}) {
		print STDERR "Cannot specify both replace & ignore. Choose one\n";
		pod2usage(1);
	}
	
	_optional_set($opts, 'fields-terminated-by', "\t");
	_optional_set($opts, 'fields-optionally-enclosed-by', q{});
	_optional_set($opts, 'lines-terminated-by', "\n");
	_optional_set($opts, 'directory', '.');
	
	return;	
}

sub _optional_set {
	my ($opts, $key, $default) = @_;	
	$opts->{$key} = $default unless $opts->{$key};
	return;
}

sub _open_dbh {
	my ($hostname, $port, $user, $password) = (
		($OPTIONS{host})     ? ";host=$OPTIONS{host}" : '',
		($OPTIONS{port})     ? ";port=$OPTIONS{port}" : '',
		($OPTIONS{user})     ? $OPTIONS{user} : undef,
		($OPTIONS{password}) ? $OPTIONS{password} : undef
	);
	my $dsn = "DBI:mysql:database=${DB}${hostname}${port};mysql_local_infile=1";
	my $dbh = DBI->connect($dsn, $user, $password, {RaiseError => 1});
	return $dbh;
}

sub _squirt_sql {
	my ($self) = @_;
	
	my ($sql) = _find_in_dir(qr/\.sql/);
	if(! -f $sql) {
		confess('No .sql file found and no schema tables detected');
	}
	
	my @args = (
		($OPTIONS{host}) ? '--host='.$OPTIONS{host} : q{},
		($OPTIONS{port}) ? '--port='.$OPTIONS{port} : q{},
		($OPTIONS{user}) ? '--user='.$OPTIONS{user} : q{},
		($OPTIONS{password}) ? '--password='.$OPTIONS{password} : q{},
		$OPTIONS{database},
		$sql
	);
	my $cmd = sprintf('mysql %s %s %s %s %s < %s', @args);
	verbose("\tRunning command '${cmd}'");
	system($cmd);
	my $exit_value  = $? >> 8;
	if($exit_value != 0) {
		confess('Exiting as mysql squirt failed');
	}
	return;
}

sub _db_tables {
	my %tables;
	my $sth = $DBH->table_info(undef, $OPTIONS{database}, '%');
	while(my $row = $sth->fetchrow_arrayref()) {
		$tables{$row->[2]} = 1;	
	}
	$sth->finish();
	return %tables;
}

#Load only the specified tables or all of them in the DB
sub _tables {
	my %tables;
	if($OPTIONS{tables}) {
		%tables = map {$_ => 1} split(/,/, $OPTIONS{tables});
	}
	else {
		%tables = _db_tables();	
	}
	return %tables;
}

sub _columns {
  my ($table) = @_;
  my @columns;
  my $sth = $DBH->column_info( undef, undef, $table, '%' );
  while ( my $row = $sth->fetchrow_arrayref() ) {
    push( @columns, {name => $row->[3], type => $row->[5], position => $row->[16]} );
  }
  $sth->finish();
  return [sort {$a->{position} <=> $b->{position}} @columns];
}

sub _find_in_dir {
	my ($file_regex) = @_;
	my @files;
	find(sub {
		my $file_name = $_;
		if($file_name =~ $file_regex) {
			push(@files, $File::Find::name);
		}
		return;
	}, $OPTIONS{directory});
	return @files;
}

sub _nuke {
  my ($table) = @_;
  if($OPTIONS{nuke}) {
    verbose("\tNuking ${table}\n");
    $DBH->do("truncate table ${table}");
  }
  return;
}

sub _load {
	my ($table, $file) = @_;
	
	my $field_opts = q{};
	if($FTB || $FEB) {
		$field_opts .= 'FIELDS ';
		if($FTB) {
			$field_opts .= q{terminated by '}.$FTB.q{' };
		}
		if($FEB) {
			$field_opts .= q{optionally enclosed by '}.$FEB.q{' };
		}
	}
	
	my @args = (
		$file,
		($OPTIONS{replace}) ? 'REPLACE' : ($OPTIONS{ignore}) ? 'IGNORE' : q{},
		$table,
		$field_opts,
		qq{lines terminated by '${LTB}'},
		($OPTIONS{'skip_lines'}) ? 'ignore '.$OPTIONS{'skip_lines'}.' lines' : q{},
		_generate_column_string($table)
	);
	
	_toggle_indexes($table, 0);
	
	my $ddl = sprintf(q{LOAD DATA LOCAL INFILE '%s' %s INTO TABLE %s %s %s %s %s}, @args);
	verbose("\t\tAbout to run $ddl");
	eval {$DBH->do($ddl);};
	if($@) {
		warn("Problem found when processing ${table} : $@");
		exit 2;
	}
	
	_toggle_indexes($table, 1);
	
	return;
}

sub _toggle_indexes {
	my ($table, $on) = @_;
	if($OPTIONS{'turn-off-indexes'}) {
		my $cmd = ($on) ? 'ENABLE' : 'DISABLE';
		verbose("\t\tToggling indexes to '$cmd'");
		my $ddl = qq{ALTER TABLE $table $cmd KEYS};
		$DBH->do($ddl);
	}
	else {
		verbose("\t\tNot toggling indexes as '--turn-off-indexes' is off");
	}
	return;
}

sub _process_records {
	my %records;
	foreach my $line (@RECORDS) {
		chomp($line);
		$records{$line} = 1;
	}
	return %records;
}

sub _is_column_binary {
  my ($column_hash) = @_;
  return ($BINARY_COLUMNS{uc($column_hash->{type})}) ? 1 : 0;
}

sub _generate_column_string {
  my ($table) = @_;
  my @columns = @{_columns($table)};
  my @column_names;
  my @sets;
  foreach my $col (@columns) {
    if(_is_column_binary($col)) {
      my $var_name = '@'.$col->{name};
      push(@sets, sprintf('%s = unhex(%s)', $col->{name}, $var_name));
      push(@column_names, $var_name);
    }
    else {
      push(@column_names, $col->{name});
    }
  }
  
  my $sets_string = (@sets) ? 'SET '.join(',',@sets) : q{};
  
  return sprintf('(%s) %s', join(',', @column_names), $sets_string);  
}

__END__

=pod

=head1 NAME

mysqlnaga

=head1 SYNOPSIS

	./mysqlnaga -database DB [-directory DIR] [-host HOST] [-port PORT] [-user USER] [-pass PASS] [more options in -h]

=head1 DESCRIPTION

Provides a Pure Perl implementation of mysqlimport using only core modules
(with the exception of DBD::mysql). This program attempts to load an entire
directory of files and optionally load a schema in which should be located in
a single .sql file.

The program loads the schema (if present) and loops over all known DB tables
looking for files in the given directory with the same name as the table 
e.g. table tmp would be used to find files called tmp.txt tmp.001.txt but not
tmp_table.txt

The script also records what it has done to aid re-runs when connections fail
under stressful loads. This is called LOADER-LOG.txt which should be edited 
accordingly when you need to run a new file.

The program also uses local file loads via the Perl DBI driver. Sometimes this
will not work because the option has not been turned on in your 
MySQL configuration. You can attempt to locally turn it on by putting
the following into ~/my.cnf	

	[client]
	loose-local-infile=1

If that does not work then consult your DBA for other possible reasons

=head1 OPTIONS

=over 8

=item B<--directory>

The directory to dump text files to. If not specified the program will use
the cwd

=item B<--database>

The database to dump. The only required parameter.

=item B<--fields-escaped-by>

Currently unimplemented as the meaning of it is ambigous in MySQL's documentation

=item B<--fields-optionally-enclosed-by>

The string to use to enclose fields. Only specify one. Defaults to blank

=item B<--fields-terminated-by>

The character(s) to indicate the end of a field. Currently defaults to tab

=item B<--replace>

If specified will force records to be replaced with those from the input file on conflicts

=item B<--ignore>

If specified will ignore clashes arising from input files

=item B<--nuke>

If specified will cause mysqlnaga-loader to truncate the table which we
are about to load into. Use with caution.

=item B<--skip-lines>

Will skip the number of given lines per table load

=item B<--host>

Host the MySQL server is located on

=item B<--lines-terminated-by>

String to indicate the end of a line i.e. the end of a record. Defaults to UNIX
line end.

=item B<--password>

Password for the target server

=item B<--port>

Port for the server

=item B<--tables>

Tables to load. If not given then all are assumed

=item B<--turn-off-indexes>

Does a bit of optimisation by turning off indexes before loading the files
which normally helps loading. Keys are turned back on after loading.

=item B<--user>

User to connect to the DB with

=item B<--verbose>

Print verbose logging messages along with the dump. These go to STDERR

=item B<--version>

Print vesion and exit

=item B<--write_header>

Writes a header line to the file which are the column names.

=item B<--help | --?>

This message

=item B<--man>

Perldoc with more information than -help alone

=back

=head1 DEPENDENCIES

Only DBD::mysql. All others are core Perl modules.

=head1 WHY NAGA

Because the program attempts to be smoking hot at copying databases. Also
naga is a lot smaller the jalepeno (and easier to remember).

=head1 AUTHOR

Andrew Yates

=head1 LICENSE

Copyright (c) 2009, ANDREW YATES
All rights reserved.

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

* Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.
* Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.
* Neither the name of the ANDREW YATES PERSONAL DEVELOPMENT nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

=head1 VERSION

1.0

=cut
